grammar org.xtext.example.oil.Oil with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate oil "http://www.xtext.org/example/oil/Oil"

OILFile:
   (version=OILVersion)?
   (include+=Include)* 
   (implementation=ImplementationDefinition)? 
   application=ApplicationDefinition;

Include:
   "#include" ("<" name=FileName ">"|name=EString);

FileName returns ecore::EString:
   FILEID;
   
OILVersion:
   "OIL_VERSION" "=" value=EString (":" description=EString)? ";";

ImplementationDefinition:
   "IMPLEMENTATION" name=Name "{" (implementationSpecs+=ImplementationSpec) "}"
   (description=EString)? ";";

ImplementationSpec:
   {ImplementationSpec}
   (osRule+=OsRule| taskRule+=TaskRule | counterRule+=CounterRule| isrRule+=IsrRule)+
;

OsRule:
   {OsRule}
   "OS" "{" (implementations+=ImplementationDef)* "}" (":" description=EString)? ";"
;
TaskRule:
   {TaskRule}
   "TASK" "{" (implementations+=ImplementationDef)* "}" (":" description=EString)? ";"
;
CounterRule:
   {CounterRule}
   "COUNTER" "{" (implementations+=ImplementationDef)* "}" (":" description=EString)? ";"
;
IsrRule:
   {IsrRule}
   "ISR" "{" (implementations+=ImplementationDef)* "}" (":" description=EString)? ";"
;

Object:
   ("OS" | "TASK" | "COUNTER" | "ALARM" | "RESOURCE" |
   "EVENT" | "ISR" | "MESSAGE" | "COM" | "NM" | 
   "APPMODE" | "IPDU" | "NETWORKMESSAGE" | "APPLICATION" | "MEMOTY_PROTECTION" | 
   "SPINLOCK" | "SCHEDULETABLE" | "IOC" | "LIBRARY")
  ;
ImplementationDef:
   ImplAttrDef | ImplRefDef;

ImplAttrDef:
   ImplAttrIntDef | ImplAttrFloatDef | ImplAttrEnumDef | ImplAttrStringDef | ImplAttrBooleanDef | ImplAttrIdentiFier;

ImplAttrIntDef:
   type=IntTypeEnum
   (auto?="WITH_AUTO")? (range=Range)? name=AttributeName (multiple?=MultipleSpecifier)?
   ("=" (value=Number | default=DefaultEnum))? (":" description=EString)? ";";

enum IntTypeEnum:
   UINT32 | INT32 | UINT64 | INT64;

ImplAttrFloatDef:
   "FLOAT"
   (auto?="WITH_AUTO")? (range=Range)? name=AttributeName (multiple?=MultipleSpecifier)?
   ("=" (value=EFloat | default=DefaultEnum))? (":" description=EString)? ";";

ImplAttrEnumDef:
   "ENUM"
   (auto?="WITH_AUTO")? (enumeration=Enumeration)? name=AttributeName (multiple?=MultipleSpecifier)?
   ("=" (value=Name | default=DefaultEnum))? (":" description=EString)? ";";

ImplAttrStringDef:
   "STRING"
   (auto?="WITH_AUTO")? name=AttributeName (multiple?=MultipleSpecifier)?
   ("=" (value=EString | default=DefaultEnum))? (":" description=EString)? ";";

ImplAttrBooleanDef:
   "BOOLEAN"
   (auto?="WITH_AUTO")? ("[" T_TRUE ("{" (trueParameterList+=ImplementationDef)* "}")?
   (trueDescription=EString)? ","
   T_FALSE
   ("{" (falseParameterList+=ImplementationDef)* "}")? (falseDescription=EString)? "]")? name=(Name | Object)
   (multiple?=MultipleSpecifier)?
   ("=" (value=EBoolean | default=DefaultEnum))? (":" description=EString)? ";";

ImplAttrIdentiFier: // possibly change according to implementation grammar.
    "IDENTIFIER"
   (auto?="WITH_AUTO")? name=AttributeName "=" value = ID ";"
;
Range:
   "[" (value=Number | begin=Number ".." end=Number | numbers+=Number ("," numbers+=Number)+) "]";

enum DefaultEnum:
   NO_DEFAULT | AUTO;

Enumeration:
   "[" list+=Enumerator ("," list+=Enumerator)* "]";

Enumerator:
   name=Name ("{" (implParameterList+=ImplementationDef)* "}")? (description=EString)?;

ImplRefDef:
   type=ObjectRefTypeEnum name=AttributeName (multiple?=MultipleSpecifier)? (description=EString)? ";";

enum ObjectRefTypeEnum:
   OS_TYPE | TASK_TYPE | COUNTER_TYPE | ALARM_TYPE | RESOURCE_TYPE | 
   EVENT_TYPE | ISR_TYPE | MESSAGE_TYPE | COM_TYPE |NM_TYPE | 
   APPMODE_TYPE | IPDU_TYPE | NETWORKMESSAGE_TYPE | APPLICATION_TYPE | MEMOTY_PROTECTION_TYPE |
   SPINLOCK_TYPE | SCHEDULETABLE_TYPE | IOC_TYPE|APICONFIG_TYPE|LIBRARY_TYPE;


MultipleSpecifier:
   "[" "]";

ApplicationDefinition:
   "CPU" name=Name "{" (objectDefinitionList+=ObjectDefinition)* "}" (description=EString)? ";";

ObjectDefinition:
   ("LIBRARY" name=Name ("{" (LibraryList += Librayattribute)* "}")? ";" |   
   object=Object name=Name ("{" (parameterList+=Attribute)* "}")? (descrption=EString)? ";");

Librayattribute :
   
   ("PATH" "=" (description=EString) ";"|
    "CHEADER" "=" (description=EString) ";"|
    "CFILE" "=" (description=EString) ";"   )
;

/************************ 
object=Object name=Name (BLOCKBEGIN (parameterList+=Attribute
   )* (reObjectDefinition=ReObjectDefinition)* BLOCKEND)? (descrption=EString)? SEMICOLON;

ReObjectDefinition://ì¶”ê?
   reobject=ReObject name=Name (BLOCKBEGIN (parameterList+=Attribute)* BLOCKEND)? (descrption=EString)? SEMICOLON;
Attribute:
   name=AttributeName "=" value=AttributeValue (description=EString)? SEMICOLON;

*************************/

Attribute:
   name=AttributeName ("=")? value=AttributeValue (description=EString)? ";";

AttributeName:
   value=(Name | Object);

AttributeValue:
   NameAttributeValue | BooleanAttributeValue | NumberAttributeValue | FloatAttributeValue | StringAttributeValue |
   AutoAttributeValue;

NameAttributeValue:
   value=Name ("{" (parameterList+=Attribute)* "}")?;

BooleanAttributeValue:
   value=EBoolean ("{" (parameterList+=Attribute)* "}")?;

NumberAttributeValue:
   value=Number;

FloatAttributeValue:
   value=EFloat;

StringAttributeValue:
   value=EString;

AutoAttributeValue:
   value?="AUTO";

EString returns ecore::EString:
   STRING;

EBoolean:
   T_TRUE | T_FALSE;

terminal T_TRUE:
   "TRUE";

terminal T_FALSE:
   "FALSE";

Number returns ecore::EInt:
   T_NUMBER;

terminal T_NUMBER:
   ("+" | "-")? (("0x" ("0".."9" | "a".."f" | "A".."F")+) | (("1".."9") ("0".."9")*) | "0");

EFloat returns ecore::EFloatObject:
   T_FLOAT;

terminal T_FLOAT:
   ("+" | "-")? ("0"..'9')* "." ("0".."9")* (("e" | "E") ("+" | "-")? ("0".."9")+)?;

Name returns ecore::EString:
   ID;

terminal ID:
   ("_" | "a".."z" | "A".."Z") ("_" | "a".."z" | "A".."Z" | "0".."9")*;
terminal FILEID:
   ("_" | "a".."z" | "A".."Z") ("_" | "a".."z" | "A".."Z" | "0".."9")* "."("a".."z" | "A".."Z") ("a".."z" | "A".."Z")*
;